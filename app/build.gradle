apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.COMPILE_SDK_VERSION

    dexOptions {
        maxProcessCount 4
        javaMaxHeapSize "3g"
    }

    defaultConfig {
        applicationId "de.mchllngr.devsettings"
        minSdkVersion rootProject.ext.MIN_SDK_VERSION
        targetSdkVersion rootProject.ext.TARGET_SDK_VERSION
        versionCode rootProject.ext.VERSIONCODE
        versionName rootProject.ext.VERSIONAME

        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.appcompat:appcompat:$rootProject.ext.APPCOMPAT_VERSION"
    implementation "com.google.android.material:material:$rootProject.ext.MATERIAL_VERSION"

    implementation "com.jakewharton:butterknife:$rootProject.ext.BUTTERKNIFE_VERSION"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.BUTTERKNIFE_VERSION"

    implementation "com.jakewharton.timber:timber:$rootProject.ext.TIMBER_VERSION"

    implementation "io.mattcarroll.hover:hover:$rootProject.ext.HOVER_VERSION"

    debugImplementation "com.facebook.stetho:stetho:$rootProject.ext.STETHO_VERSION"

    debugImplementation "io.palaima.debugdrawer:debugdrawer:$rootProject.ext.DEBUGDRAWER_VERSION"
    debugImplementation "io.palaima.debugdrawer:debugdrawer-base:$rootProject.ext.DEBUGDRAWER_VERSION"
    debugImplementation "io.palaima.debugdrawer:debugdrawer-commons:$rootProject.ext.DEBUGDRAWER_VERSION"
    debugImplementation "io.palaima.debugdrawer:debugdrawer-actions:$rootProject.ext.DEBUGDRAWER_VERSION"
    debugImplementation "io.palaima.debugdrawer:debugdrawer-scalpel:$rootProject.ext.DEBUGDRAWER_VERSION"
    debugImplementation "io.palaima.debugdrawer:debugdrawer-fps:$rootProject.ext.DEBUGDRAWER_VERSION"
    // Used by DebugDrawer
    debugImplementation "com.jakewharton.scalpel:scalpel:$rootProject.ext.SCALPEL_VERSION"
    debugImplementation "jp.wasabeef:takt:$rootProject.ext.TAKT_VERSION"
}
